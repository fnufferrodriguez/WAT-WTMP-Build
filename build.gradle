plugins {
    id 'wtmp.deps-conventions'
    id "java"
    id 'wtmp.versioning-conventions'
    id 'wtmp.publishing-conventions'
}

allprojects {
    group = 'gov.usbr'
    version = versionConventions.getConventionVersion()
}

configurations {
    // The base WAT install
    wat_install
    // The extra upstream WAT plugins
    wat_plugins
    // The specific ResSim we want to use
    ressim_install
    // W2 Pre EXE, fixed name
    w2_pre_exe
    // W2 Post EXE, fixed name
    w2_post_exe
    // CeQualW2 executable
    cequalw2_exe
    // The TCD Variant required sometimes
    cequalw2_TCD_exe
    // Another TCD variant required sometimes
    cequalw2_TCD_0817_exe
    // v5 TCD variant
    cequalw2_TCD_v5_exe
    // CeQualW2 4.5 executable set
    cequalw2_45_exes
    // Folsom Exe required by some users
    cequalw2_folsom_exe
    // Extra Jars
    addl_wat_jars
    // Extra Jars
    addl_wat_firstparty_jars
    // The USBR WAT Plugins
    usbr_plugins
    // Python Report Generating code
    usbr_python_report
    // Python Git Tool
    usbr_python_git
    // Jasper files
    usbr_jasper
    // Additional WAT Natives
    addl_wat_natives
}

dependencies {
    wat_install(libs.wat.install) {
        artifact {
            type = "zip"
        }
    }

    wat_plugins libs.bundles.wat.plugins

    w2_pre_exe(libs.w2.pre) {
        artifact {
            type = "exe"
        }
    }
    w2_post_exe(libs.w2.post) {
        artifact {
            type = "exe"
        }
    }
    cequalw2_exe(libs.cequalw2) {
        artifact {
            type = "exe"
        }
    }
    cequalw2_TCD_exe(libs.cequalw2.tcd) {
        artifact {
            type = "exe"
        }
    }
    cequalw2_TCD_0817_exe(libs.cequalw2.tcd0817) {
        artifact {
            type = "exe"
        }
    }
    cequalw2_TCD_v5_exe(libs.cequalw2.v5.tcd) {
        artifact {
            type = "exe"
        }
    }

    cequalw2_45_exes(libs.cequalw2.v45) {
        artifact {
            type = "exe"
        }
    }
    cequalw2_45_exes(libs.cequalw2preconsole.v45) {
        artifact {
            type = "exe"
        }
    }
    cequalw2_45_exes(libs.cequalw2pre.v45) {
        artifact {
            type = "exe"
        }
    }

    cequalw2_folsom_exe(libs.cequalw2.folsom) {
        artifact {
            type = "exe"
        }
    }

    ressim_install(libs.ressim.install) {
        artifact {
            type = "zip"
        }
    }

    usbr_plugins(libs.bundles.usbr.plugins){
        exclude group: "mil.army.usace.hec"
        exclude group: "com.rmanet", module: "rmadev"
    }

    usbr_jasper(libs.usbr.actionpanel.jasper) {
        artifact {
            type = "zip"
        }
    }
    addl_wat_jars libs.jasper.fonts

    // Filtering out Jackson jars from WAT drops this jar, which USBR Plugins do not pull in. Add it back
    addl_wat_jars libs.jackson.json
    addl_wat_jars libs.jarhdf5

    // This is a hack until ResSim publishes a proper Gradle Platform/Maven BOM. This needs to be revisited them.
    addl_wat_firstparty_jars(libs.ressim.gvdl) {
        exclude group: '*'
    }

    addl_wat_natives libs.jhdf5.win.x8664

    usbr_python_report(libs.usbr.actionpanel.python) {
        artifact {
            type = "zip"
        }
    }

    usbr_python_git(libs.usbr.wtmp.git) {
        artifact {
            type = "zip"
        }
    }

    configurations.usbr_plugins.exclude group: "mil.army.usace.hec.wat"
}

static boolean filterOriginalWatZip(FileTreeElement fvd) {
    // We don't use FIA, HMS
    def path = fvd.getRelativePath();
    return path.contains("/apps/") || path.contains("S25FIAPlugin.jar") ||
            path.contains("S10HMSPlugin") || path.contains("S15ResSimPlugin-v3.5") ||
            path.contains("S20RASPlugin") || path.contains("fcPlugin-1.1-") ||
            path.contains("hsPlugin-1.1-") || path.contains("sdi-1.1-") ||
            path.contains("pm-1.1-") || path.contains("wat-plugin-ressim-3.5") ||
            // Exclude older Jackson jars that come from WAT, since USBR Plugins include newer Jackson jars
            (path.contains("jackson-") && path.endsWithIgnoreCase("-2.11.0.jar")) ||
            path.contains("gvdl-util")
}

def getGVDLUtilJarName() {
    libs.ressim.gvdl.get().module.name + "-" + libs.versions.ressim.gvdl.get() + ".jar"
}

tasks.register("extractWAT", Sync) { sync ->
    group = "build"
    description = "Extract the original, unmodified WAT install package which will be used as the WTMP base"

    sync.into(layout.buildDirectory.dir("originalWAT"))

    sync.dependsOn(configurations.wat_install)
    sync.from { configurations.wat_install.collect { file -> zipTree(file) } }
}

tasks.register("filterWAT", Sync) { sync ->
    group = "build"
    description = "Filter components out of the original WAT install which are not used by WTMP"

    sync.into(layout.buildDirectory.dir("filteredWAT"))

    sync.dependsOn(tasks.named("extractWAT"))
    sync.from(tasks.named("extractWAT")) { copySpec ->
        copySpec.exclude { fvd -> filterOriginalWatZip(fvd) }
        copySpec.eachFile { fcd ->
            def segmentList = fcd.relativePath.segments.toList()
            // Duplicate HEC-WAT entry
            segmentList.remove(0)
            fcd.relativePath = new RelativePath(true, (String[]) (segmentList.toArray()))
        }
        copySpec.includeEmptyDirs = false
    }
}

tasks.register("extractResSim", Sync) { sync ->
    group = "build"
    description = "Extract the original, unmodified ResSim build that will be included in WTMP"

    sync.into(layout.buildDirectory.dir("originalRSS"))

    sync.dependsOn(configurations.ressim_install)
    sync.from { configurations.ressim_install.collect { file -> zipTree(file) } }
}

def extraFilesSourceSet = objects.sourceDirectorySet("extraInstallFiles", "extraInstallFiles")
extraFilesSourceSet.srcDir("src/main/install")

tasks.register("assembleInstaller", Sync) { task ->
    group = 'application'
    description = 'Build the WTMP Installer Package'

    task.into layout.buildDirectory.dir("WTMP")

    task.dependsOn(tasks.named("filterWAT"))
    task.dependsOn(configurations.usbr_python_report)
    task.dependsOn(configurations.usbr_jasper)
    task.dependsOn(configurations.usbr_python_git)
    task.dependsOn(extraFilesSourceSet)

    // The base WAT installer
    task.into(".") { copySpec ->
        // WAT without config files that need edited
        copySpec.from(tasks.named("filterWAT")) { filteredWatSpec ->
            filteredWatSpec.exclude '**/*.config'
            filteredWatSpec.exclude '**/*.template'
            filteredWatSpec.exclude '**/*.properties'
        }
        // Copy the config / template files that get modified
        copySpec.from(tasks.named("filterWAT")) { filteredWatSpec ->
            filteredWatSpec.include '**/*.config'
            filteredWatSpec.include '**/*.template'
            filteredWatSpec.filter { line ->
                {
                    var updatedLine = line.replaceAll('-Xmx6g', '-Xmx10g')
                    if (updatedLine.contains("-Dapp.Version")) {
                        // app.version is the last line in WAT config, so append this to the end
                        updatedLine += ("\n\n" +
                                "# +--------------------------+\n" +
                                "# | WTMP Specific Parameters |\n" +
                                "# +--------------------------+\n" +
                                "vmparam -Djavax.net.ssl.trustStoreType=WINDOWS-ROOT\n" +
                                "vmparam -Dw2.iterative.compute=true\n" +
                                "vmparam -Dlogback.configurationFile=./config/properties/logback.xml\n" +
                                "vmparam -DEnableSkipModelsMenu=true\n" +
                                "#vmparam -DWTMP.HasUpdateData=true\n" +
                                "#vmparam -DWTMP.HasGit=true")
                    }
                    if (updatedLine.contains("gvdl-util")) {
                        updatedLine = "addjar jar/" + getGVDLUtilJarName()
                    }
                    return updatedLine
                }
            }
        }
        // Copy the properties files that get modified
        copySpec.from(tasks.named("filterWAT")) { filteredWatSpec ->
            filteredWatSpec.include '**/*.properties'
            filteredWatSpec.filter { line ->
                {
                    var updatedLine = line.replaceAll('FINER', 'INFO').replaceAll('FINEST', 'INFO').replaceAll('FINE', 'INFO')
                    return updatedLine
                }
            }
        }
        // WTMP-specific addon files
        from(extraFilesSourceSet)

        copySpec.from { configurations.usbr_python_report.collect { file -> zipTree(file) } }
        copySpec.from { configurations.usbr_jasper.collect { file -> zipTree(file) } }
        copySpec.from { configurations.usbr_python_git.collect { file -> zipTree(file) } }
    }

    task.dependsOn(configurations.addl_wat_firstparty_jars)
    task.into("HEC-WAT/jar") { copySpec ->
        from configurations.addl_wat_firstparty_jars.copy()
    }

    task.dependsOn(configurations.addl_wat_jars)
    task.dependsOn(configurations.usbr_plugins)
    task.into("HEC-WAT/jar/sys") { copySpec ->
        // Additional jars into WAT/jar (eg Jasper fonts)
        copySpec.from configurations.addl_wat_jars.copy().setTransitive(false)
        // Plugin dependencies go into jar/sys
        copySpec.from configurations.usbr_plugins.copyRecursive().setTransitive(true) - configurations.usbr_plugins.copy().setTransitive(false)
    }

    task.dependsOn(configurations.wat_plugins)
    // The plugin jars go in jar/ext
    task.into("HEC-WAT/jar/ext") { intoDir ->
        from { configurations.wat_plugins.copy().setTransitive(false) }
        from { configurations.usbr_plugins.copy().setTransitive(false) }
    }

    // Additional natives into WAT/lib (eg Jasper fonts)
    task.dependsOn(configurations.addl_wat_natives)
    task.into("HEC-WAT/lib") { copySpec ->
        copySpec.from { configurations.addl_wat_natives.collect { file -> zipTree(file) } }
    }

    task.dependsOn(tasks.named("extractResSim"))
    // The right version of ResSim
    task.into("apps/HEC-ResSim") { intoDir ->
         intoDir.from(tasks.named("extractResSim")) { rssCopySpec ->
            rssCopySpec.eachFile { fcd ->
                def segmentList = fcd.relativePath.segments.toList()
                // Duplicate HEC-ResSim entry
                segmentList.remove(2)
                fcd.relativePath = new RelativePath(true, (String[]) (segmentList.toArray()))
            }
            rssCopySpec.includeEmptyDirs = false
        }
    }

    task.into("apps/ceQualW2/exe") { intoDir ->
        // These will take whatever name is grabbed out of Nexus
        intoDir.from configurations.cequalw2_exe
        intoDir.from configurations.cequalw2_TCD_exe
        intoDir.from(configurations.w2_pre_exe) { fromFile ->
            // Explicitly named as the WAT Plugin looks for this.
            fromFile.rename { "Console_PreW2.exe" }
        }
        intoDir.from(configurations.w2_post_exe) { fromFile ->
            // Explicitly named as the WAT Plugin looks for this.
            fromFile.rename { "W2_Post3.exe" }
        }
        intoDir.from(configurations.cequalw2_TCD_0817_exe) { fromFile ->
            // Explicitly named as the WAT Plugin looks for this.
            fromFile.rename { "W2_TCD_081721.exe" }
        }
    }

    task.into("apps/ceQualW2/exec_45") { intoDir ->
        // These will take whatever name is grabbed out of Nexus
        intoDir.from configurations.cequalw2_45_exes
        intoDir.from(configurations.cequalw2_folsom_exe) { fromFile ->
            // Explicitly named as the WAT Plugin looks for this.
            fromFile.rename { "w2_v45_64_Folsom.exe" }
        }
    }

    task.into("apps/ceQualW2/exec_50") { intoDir ->
        // These will take whatever name is grabbed out of Nexus
        intoDir.from(configurations.cequalw2_TCD_v5_exe){ fromFile ->
            // Explicitly named as the WAT Plugin looks for this.
            fromFile.rename { "w2_v5tcd09262025.exe" }
        }
    }

    // There will be duplicates between WAT and the plugins. Just ignore them for now.
    task.duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

tasks.register("zipWtmpInstaller", Zip){
    group = 'application'
    description = 'Assembles and Zips the WTMP Install'

    dependsOn tasks.named("assembleInstaller")

    from tasks.named("assembleInstaller")

    def baseName = 'WTMP'
    def version_no_platform = versionConventions.getShortVersionName() + versionConventions.getBuildID()
    def version = version_no_platform + "-win-x86_64"

    destinationDirectory = file("$buildDir")
    archiveBaseName = baseName
    archiveVersion = version
    archiveExtension = 'zip'

    def buildSpecificSubdir = baseName + '-' + version_no_platform

    includeEmptyDirs false

    // In the archive, put it in a subfolder with the build name
    eachFile { fileCopyDetails ->
        fileCopyDetails.setRelativePath(fileCopyDetails.getRelativePath().prepend(buildSpecificSubdir))
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = "usbr-wtmp-package"
            def verWithExt = versionConventions.getShortVersionName() + "-win-x86_64"
            if(project.version.contains("SNAPSHOT")) {
                verWithExt += "-SNAPSHOT"
            }
            it.version = verWithExt
            artifact source: zipWtmpInstaller, extension: 'zip'
        }
    }
}
